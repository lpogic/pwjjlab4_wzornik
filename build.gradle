plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.7'
}

group 'palma'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

javafx {
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

mainClassName = "$moduleName/${main}"

def java_home = hasProperty('org.gradle.java.home') ? getProperty('org.gradle.java.home') : System.getenv('JAVA_HOME')
def fx_jmods = hasProperty('path.to.fx.mods') ? getProperty('path.to.fx.mods') : System.getenv('PATH_TO_FX_MODS')

task jlink(type: Exec) {
    dependsOn 'clean'
    dependsOn 'jar'

    workingDir 'build'

    if (java_home == null) {
        throw new RuntimeException("Nie moge zlokalizowac jdk dodaj org.gradle.java.home w gradle.properties.")
    }
    if (fx_jmods == null) {
        throw new RuntimeException("Nie moge zlokalizowac jfx-mods dodaj path.to.fx.mods w gradle.properties.")
    }
    commandLine "${java_home}/bin/jlink", '--module-path', "libs${File.pathSeparatorChar}${fx_jmods}",
            '--add-modules', "${moduleName}", '--output', "${moduleName}", '--strip-debug',
            '--compress', '2', '--no-header-files', '--no-man-pages','--launcher',"launch=${mainClassName}"
}
